#TIMEHORIZON
T = 8760 * 1;

#NODE BIOMASS
// ASSET data for 2050 have been taken as defaults for cost and efficiency parameters 

#PARAMETERS
unit_GW = 0;
energy_electricity = 0;
pre_installed_capacity = 0.215;         // GW(e)
max_capacity = max_capacity;            // GW(e)
// data from ASSET 2020
capex_existing = 2000;                  // M€/GW(e)
fom_existing = 47.5;                    // M€/GW(e)/yr
vom_existing = 3.56 * 1/(10**3);        // M€/GWh(e)
lifetime_existing = 40;                 // years
// data from ASSET 2050
capex = 1700;                           // M€/GW(e)
fom = 38.4;                             // M€/GW(e)/yr
vom = 3.56 * 1/(10**3);                 // M€/GWh(e)
conversion_efficiency = 0.4;            // kWh(e)/kWh(biomass)
lifetime = 40;                          // years
// Other
fuel_cost = 35.0 * 1/(10**3);           // k€/MWh(e) from Berger et al. 2019
min_external_power = 0.45;              // from ens.df
planned_outage = 3/52;                  // weeks/52, data from ens.dk biomass chp                   
// data from ens.dk biomass chp
spec_CO2_emission = 0.4;                // kt(co2)/GWh(biomass) data from Berger et al. 2019
ramp_rate_up = 0.25;                    // -/h from Berger et al. 2019
ramp_rate_down = 0.3;                   // -/h from Berger et al. 2019
nb_year = T/8760;
minimum_capacity_factor = 0.45; 	
// yearly capex
wacc = 0.07;
yearly_capex = capex * wacc / (1 - (1 + wacc)**(-lifetime));                                            // M€/GW(e)/yr
// used to compute the total cost of the system after the optimisation
yearly_capex_existing = capex_existing * wacc / (1 - (1 + wacc)**(-lifetime_existing));                 // M€/GW(e)/yr
yearly_existing_cost = (yearly_capex_existing + fom_existing) * pre_installed_capacity;                 // M€/yr
// Conversion factors
conversion_factor_biomass = conversion_efficiency;                                                      // GWh(e)/GWh(biomass)
conversion_factor_carbon_dioxide = conversion_factor_biomass/spec_CO2_emission;                         // GWh(e)/kt(co2)

#VARIABLES
internal: new_capacity;                 // GW(e)
external: e_produced[T];                // GWh(e) net production of electricity
external: co2_produced[T];              // kt(co2)

#CONSTRAINTS
new_capacity >= 0;
pre_installed_capacity + new_capacity <= max_capacity;
min_external_power * (new_capacity + pre_installed_capacity) <= e_produced[t];
e_produced[t] <= (new_capacity + pre_installed_capacity); 
e_produced[i] - e_produced[i-1] <= ramp_rate_up * (new_capacity + pre_installed_capacity) for i in [1:T-1];
e_produced[i] - e_produced[i-1] >= - ramp_rate_down * (new_capacity + pre_installed_capacity) for i in [1:T-1];
sum(e_produced[i] for i in [0:T-1]) <= (1 - planned_outage) * (pre_installed_capacity + new_capacity) * T;
co2_produced[t] == e_produced[t]/conversion_factor_carbon_dioxide;
sum(e_produced[i] for i in [0:T-1]) >= minimum_capacity_factor * (pre_installed_capacity + new_capacity) * T;

#OBJECTIVES
min fix_cost: (yearly_capex + fom) * nb_year * new_capacity ; 		                        // M€	
min var_cost: vom * e_produced[t];                                                          // M€
min fuel_cost: fuel_cost * e_produced[t]/conversion_factor_biomass;                         // M€
