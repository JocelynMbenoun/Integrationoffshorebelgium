#TIMEHORIZON
T = 8760;

#NODE CHP
// ens.dk data for 2050 for the gas engines have been taken as defaults for cost and efficiency parameters 

#PARAMETERS
unit_GW = 0;
energy_electricity = 0;
pre_installed_capacity = 0.686;             // GW(e)
max_capacity = pre_installed_capacity;      // GW(e)
// data from ens.dk 2020
capex_existing = 950;                       // M€/GW(e)
fom_existing = 9.75;                        // M€/GW(e)/yr
vom_existing = 5.4 * 1/(10**3);             // M€/GWh(e)
conversion_efficiency_existing = 0.47;      // GWh(e)/GWh(ch4_hhv)
lifetime_existing = 25;                     // years
// data from ens.dk 2050
capex = 850;                                // M€/GW(e)
fom = 8.5;                                  // M€/GW(e)/yr
vom = 4.9 * 1/(10**3);                      // M€/GWh(e)
conversion_efficiency = 0.5;                // GWh(e)/GWh(ch4_hhv)
lifetime = 25;                              // years
planned_outage = 0.8/52;                    // weeks/52 
// Others
min_external_power = 0.0;                   // 0.5 in ens.dk but used 0 here for now 
ramp_rate_up = 0.25;                         
ramp_rate_down = 0.3;                    
spec_CO2_emission = 0.202;                  // kt/GWh or t/MWh
nb_year = T/8760;
minimum_capacity_factor = 0.5; 		
// yearly capex
wacc = 0.07;
yearly_capex = capex * wacc / (1 - (1 + wacc)**(-lifetime));                                            // M€/GW(e)/yr
// used to compute the total cost of the system after the optimisation
yearly_capex_existing = capex_existing * wacc / (1 - (1 + wacc)**(-lifetime_existing));                 // M€/GW(e)/yr
yearly_existing_cost = (yearly_capex_existing + fom_existing) * pre_installed_capacity;                 // M€/yr
// Conversion factors
conversion_factor_methane = conversion_efficiency;                                                      // GWh(e)/GWh(ch4)
conversion_factor_carbon_dioxide = conversion_factor_methane/spec_CO2_emission;                         // GWh(e)/kt(co2)

#VARIABLES
internal: new_capacity;                 // GW(e)
external: ng_consumed[T];               // GWh(ch4)
external: e_produced[T];                // GWh(e)
external: co2_produced[T];              // kt(co2)

#CONSTRAINTS
new_capacity >= 0;
pre_installed_capacity + new_capacity <= max_capacity;
min_external_power * (new_capacity + pre_installed_capacity) <= e_produced[t];
e_produced[t] <= (new_capacity + pre_installed_capacity);
e_produced[t] == conversion_factor_methane * ng_consumed[t];
e_produced[i] - e_produced[i-1] <= ramp_rate_up * (new_capacity + pre_installed_capacity) for i in [1:T-1];
e_produced[i] - e_produced[i-1] >= - ramp_rate_down * (new_capacity + pre_installed_capacity) for i in [1:T-1];
sum(e_produced[i] for i in [0:T-1]) <= (1 - planned_outage) * (pre_installed_capacity + new_capacity) * T;
co2_produced[t] == e_produced[t]/conversion_factor_carbon_dioxide;
sum(e_produced[i] for i in [0:T-1]) >= minimum_capacity_factor * (pre_installed_capacity + new_capacity) * T;

#OBJECTIVES
min fix_cost: (yearly_capex + fom) * nb_year * new_capacity ; 		// M€	
min var_cost: vom * e_produced[t];                                  // M€