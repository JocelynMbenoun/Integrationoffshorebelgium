#TIMEHORIZON
T = 8760 * 1;

#NODE HVAC
// National Grid. Electricity ten year statement 2015 - appendix E

#PARAMETERS
unit_GW = 0;
energy_electricity = 0;
pre_installed_capacity = 0.0;                                                       // GW(e)
max_capacity = 100;                                                                 // GW(e)
max_power = 1;                                                                      // GW(e)
length = 40;                                                                        // km
capex_lines_existing = 4.57*length;                                                 // M€/GW(e)
capex_stations_existing = 217.2;                                                    // M€/GW(e)
capex_existing = capex_lines_existing + capex_stations_existing;                    // M€/GW(e)
fom_existing = 0.015* capex_existing;                                               // M€/GW(e)/yr
vom_existing = 0.000001;                                                            // M€/GWh(e)
lifetime_existing = 40;                                                             // years
capex_lines = 4.57*length;                                                          // M€/GW(e)
capex_stations = 217.2;                                                             // M€/GW(e)
capex = capex_lines + capex_stations;                                               // M€/GW(e)
fom = 0.015 * capex;                                                                 // M€/GW(e)/yr
vom = 0.000001;                                                                     // M€/GWh(e)
lifetime_lines = 40.0;                                                              // years    
lifetime_stations = 40.0;                                                           // years
lifetime = 40;
efficiency_HVAC = 0.93;
nb_year = T/8760;
// yearly capex
wacc = 0.07;
yearly_capex = capex * wacc / (1 - (1 + wacc)**(-lifetime));                                            // M€/GW(e)/yr
// used to compute the total cost of the system after the optimisation
yearly_capex_existing = capex_existing * wacc / (1 - (1 + wacc)**(-lifetime_existing));                 // M€/GW(e)/yr
yearly_existing_cost = (yearly_capex_existing + fom_existing) * pre_installed_capacity;                 // M€/yr


#VARIABLES
internal: new_capacity;                                                             // GW(e)
external: e_forward_in[T];                                                          // GWh(e)
external: e_forward_out[T];                                                         // GWh(e)
external: e_reverse_in[T];                                                          // GWh(e)
external: e_reverse_out[T];                                                         // GWh(e)

#CONSTRAINTS
new_capacity >= 0;
new_capacity + pre_installed_capacity <= max_capacity;
e_forward_in[t] >= 0;
e_reverse_in[t] >= 0;
e_forward_in[t] <= max_power * (new_capacity + pre_installed_capacity);
e_reverse_in[t] <= max_power * (new_capacity + pre_installed_capacity);
e_forward_out[t] == efficiency_HVAC * e_forward_in[t]; 
e_reverse_out[t] == efficiency_HVAC * e_reverse_in[t];


#OBJECTIVES
min fix_cost: (yearly_capex + fom) * nb_year * new_capacity ; 		                // M€	
min var_cost: vom * (e_forward_in[t] + e_reverse_in[t]);                            // M€